apply plugin: "java"
apply plugin: "war"
apply plugin: "gwt"
apply plugin: "eclipse"

sourceCompatibility = 7
targetCompatibility = 7

ext.springVersion = "4.0.2.RELEASE"
ext.aspectjVersion = "1.7.4"

repositories {
	mavenCentral()
}

sourceSets {
	generated
}

sourceSets.generated.java.srcDirs = ["src/main/generated"]

sourceSets.main.java.srcDirs += sourceSets.generated.java.srcDirs

configurations {
	requestfactoryValidator
}

buildscript {
	repositories {
		maven {
			url "https://github.com/steffenschaefer/gwt-gradle-plugin/raw/maven-repo/"
		}
		
		maven {
			url "https://maven.eveoh.nl/content/repositories/releases"
		}
		mavenCentral()
	}
	dependencies {
		classpath "de.richsource.gradle.plugins:gwt-gradle-plugin:0.3"
		classpath "nl.eveoh:gradle-aspectj:1.3"
	}
}

apply plugin: "aspectj"

configurations {
	all*.exclude group: "commons-logging", module: "commons-logging"
}

gwt {
	gwtVersion="2.6.0"

	modules "ch.unibas.medizin.logbook.LogBookEntry"

	compiler {
		strict = false;
		enableClosureCompiler = true;
		disableClassMetadata = true;
		disableCastChecking = true;
	}
}

dependencies {
	// GWT
	compile "com.google.gwt:gwt-servlet:2.6.0"
	compile "com.google.gwt.inject:gin:2.1.2"
	compile "de.novanic.gwteventservice:gwteventservice:1.2.1"

	// Spring
	compile "org.springframework:spring-webmvc:${springVersion}"
	compile "org.springframework:spring-web:${springVersion}"
	compile "org.springframework:spring-tx:${springVersion}"
	compile "org.springframework:spring-context-support:${springVersion}"
	compile "org.springframework:spring-orm:${springVersion}"
	compile "org.springframework:spring-aop:${springVersion}"
	compile "org.springframework:spring-aspects:${springVersion}"
	
	// JPA
	compile "org.hibernate:hibernate-entitymanager:4.3.1.Final"
	compile "javax.validation:validation-api:1.0.0.GA:sources"
	compile ("org.hibernate:hibernate-validator:4.2.0.Final") {
		exclude(module: "jaxb-api")
		exclude(module: "jaxb-impl")
	}
	compile "org.apache.tomcat:tomcat-jdbc:7.0.50"
	runtime "com.h2database:h2:1.3.175"
	runtime "mysql:mysql-connector-java:5.1.29"
	
	// Apache Commons
	compile "org.apache.commons:commons-lang3:3.1"
	compile "commons-collections:commons-collections:3.2.1"
	compile "commons-digester:commons-digester:2.1"
	compile "commons-fileupload:commons-fileupload:1.3.1"
	compile "commons-codec:commons-codec:1.9"
	compile "commons-io:commons-io:2.4"
	compile "org.apache.commons:commons-math:2.2"

	// Logging
	compile "com.allen-sauer.gwt.log:gwt-log:3.3.0"
	compile "org.slf4j:jcl-over-slf4j:1.7.6"
	compile "org.slf4j:log4j-over-slf4j:1.7.6"
	compile "ch.qos.logback:logback-classic:1.1.1"
	
	//compile "org.aspectj:aspectjrt:${aspectjVersion}"
	//compile "org.aspectj:aspectjweaver:${aspectjVersion}"
	
	// Rendering
	compile "net.sourceforge.javacsv:javacsv:2.0"
	compile "org.xhtmlrenderer:core-renderer:R8pre2"
	compile "com.lowagie:itext:2.0.8"
	compile "com.itextpdf:itextpdf:5.5.0"
	
	runtime "org.json:json:20140107"
 
	providedCompile "javax.servlet:javax.servlet-api:3.0.1"

	aspectpath "org.springframework:spring-aspects:${springVersion}"

	requestfactoryValidator "com.google.web.bindery:requestfactory-apt:2.6.0"
}

task generateRequetfacoryValidation(type: JavaCompile, group: "build", description: "Generates the RequetfacoryValidation") {
	source = sourceSets.main.java
	classpath = configurations.compile + configurations.requestfactoryValidator
	options.compilerArgs = [
		"-proc:only",
		"-processor", "com.google.web.bindery.requestfactory.apt.RfValidator"
	]
	destinationDir = sourceSets.generated.java.srcDirs.iterator().next()
}

compileAspect.dependsOn generateRequetfacoryValidation
